project.ext.jacocoEnabled = project.gradle.startParameter.taskNames.contains('jacocoRootReport')

if (jacocoEnabled) {
    allprojects {
        apply plugin: 'jacoco'

        clean {
            doFirst {
                file("$buildDir/jacoco").deleteDir()
                file("$buildDir/reports/jacoco").deleteDir()
            }
        }
        
        repositories {
            mavenCentral()
        }
    }

    configure(javaProjects) {
        test {
            reports {
                html.enabled = false
            }
        }
    }

    task jacocoRootReport(type: JacocoReport) {
        additionalSourceDirs.from = files(javaProjects.sourceSets.main.allSource.srcDirs)
        sourceDirectories.from = files(javaProjects.sourceSets.main.allSource.srcDirs)
        classDirectories.from = files(javaProjects.sourceSets.main.output)
        executionData.from = files(javaProjects.jacocoTestReport.executionData)

        reports {
            html.enabled = false
            csv.enabled = false
            xml.enabled = true
        }
        onlyIf = {
            true
        }

        doFirst {
            executionData.from = files(executionData.findAll {
                it.exists()
            })
        }
    }
}